buildscript {
  repositories {
    google()
  }
}
apply plugin: 'com.android.application'
apply plugin: 'devicefarm'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 29)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId 'host.exp.exponent'
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 29)
    // ADD VERSIONS HERE
    // BEGIN VERSIONS
    versionCode 128
    versionName '2.16.0'
    // END VERSIONS

    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    // Deprecated. Used by net.openid:appauth
    manifestPlaceholders = [
        'appAuthRedirectScheme': 'host.exp.exponent'
    ]
  }
  dexOptions {
    javaMaxHeapSize System.getenv("DISABLE_DEX_MAX_HEAP") ? null : "8g"
  }

  signingConfigs {
    debug {
      storeFile file('../debug.keystore')
    }
    release {
      storeFile file(System.getenv("ANDROID_KEYSTORE_PATH") ?: "release-key.jks")
      storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
      keyAlias System.getenv("ANDROID_KEY_ALIAS")
      keyPassword System.getenv("ANDROID_KEY_PASSWORD")
    }
  }
  buildTypes {
    debug {
      debuggable true
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      consumerProguardFiles 'proguard-rules.pro'
      if (!System.getenv("ANDROID_UNSIGNED")) {
        signingConfig signingConfigs.release
      }
    }
  }
  lintOptions {
    abortOnError false
  }
  packagingOptions {
    pickFirst "**"
  }
}

devicefarm {
  projectName System.getenv("DEVICEFARM_PROJECT_NAME")
  devicePool System.getenv("DEVICEFARM_DEVICE_POOL")
  executionTimeoutMinutes 40
  authentication {

    accessKey System.getenv("AWS_ACCESS_KEY_ID")
    secretKey System.getenv("AWS_SECRET_ACCESS_KEY")
  }
}

configurations.all {
  resolutionStrategy {
    force 'org.webkit:android-jsc:r245459'
  }
// WHEN_DISTRIBUTING_REMOVE_FROM_HERE
  resolutionStrategy.dependencySubstitution {
    substitute module("com.facebook.react:react-native:+") with project(":ReactAndroid")
  }
// WHEN_DISTRIBUTING_REMOVE_TO_HERE
}

// WHEN_PREPARING_SHELL_REMOVE_FROM_HERE
/* UNCOMMENT WHEN DISTRIBUTING
apply from: 'expo.gradle'
END UNCOMMENT WHEN DISTRIBUTING */
// WHEN_PREPARING_SHELL_REMOVE_TO_HERE

apply from: "../../node_modules/react-native-unimodules/gradle.groovy"

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation 'androidx.multidex:multidex:2.0.0'

  // Our dependencies
  implementation 'androidx.appcompat:appcompat:1.1.0'

  // Our dependencies from ExpoView
  // DON'T ADD ANYTHING HERE THAT ISN'T IN EXPOVIEW. ONLY COPY THINGS FROM EXPOVIEW TO HERE.
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation('com.facebook.android:audience-network-sdk:5.1.1') {
    exclude module: 'play-services-ads'
  }
  compileOnly 'org.glassfish:javax.annotation:3.1.1'
  implementation 'com.jakewharton:butterknife:10.2.1'
  implementation 'de.greenrobot:eventbus:2.4.0'

  implementation 'com.squareup.picasso:picasso:2.5.2'
  implementation 'com.google.android.gms:play-services-gcm:15.0.1'
  implementation 'com.google.android.gms:play-services-analytics:16.0.1'
  implementation 'com.google.android.gms:play-services-maps:15.0.1'
  implementation 'com.google.android.gms:play-services-auth:15.0.1'
  implementation 'com.google.android.gms:play-services-location:15.0.1'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  // debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  implementation 'com.facebook.device.yearclass:yearclass:2.1.0'
  implementation 'commons-io:commons-io:1.4'
  implementation 'me.leolin:ShortcutBadger:1.1.4@aar'
  implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
  implementation 'commons-codec:commons-codec:1.10'
  implementation 'com.segment.analytics.android:analytics:4.3.0'
  implementation 'com.google.zxing:core:3.3.3'
  implementation 'net.openid:appauth:0.4.1'
  implementation 'com.airbnb.android:lottie:2.5.6'
  implementation('io.nlopez.smartlocation:library:3.2.11') {
    transitive = false
  }
  implementation "androidx.exifinterface:exifinterface:1.0.0"
  implementation 'com.squareup.okio:okio:1.9.0'
  implementation 'com.facebook.soloader:soloader:0.8.2'
  implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

  // expo-file-system
  implementation 'com.squareup.okhttp3:okhttp:3.10.0'
  implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'

  // Testing
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
  // We use a modified build of com.android.support.test:runner:1.0.1. Explanation in maven-test/README
  androidTestImplementation 'androidx.test:runner:1.1.0'
  androidTestImplementation "androidx.annotation:annotation:1.0.0"
  androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.0'
  androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
  androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'

  androidTestImplementation 'junit:junit:4.12'
  androidTestImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'org.robolectric:robolectric:3.8'
  androidTestImplementation 'androidx.test:runner:1.1.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'

  testImplementation 'androidx.test:runner:1.1.0'
  testImplementation 'androidx.test:rules:1.2.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:1.10.19'

  /* UNCOMMENT WHEN DISTRIBUTING
  implementation('host.exp.exponent:expoview:38.0.0@aar') {
    transitive = true
    exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    exclude group: 'com.squareup.okhttp3', module: 'okhttp-urlconnection'
  }
  END UNCOMMENT WHEN DISTRIBUTING */

  // WHEN_DISTRIBUTING_REMOVE_FROM_HERE

  // BEGIN_SDK_UNVERSIONED
  implementation(project(':expoview'))

  implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
  // END_SDK_UNVERSIONED

  // WHEN_PREPARING_SHELL_REMOVE_FROM_HERE

  // ADD_NEW_SDKS_HERE


  // BEGIN_SDK_38
  implementation(project(':expoview-abi38_0_0'))
  // END_SDK_38


  // BEGIN_SDK_37
  implementation(project(':expoview-abi37_0_0'))
  // END_SDK_37

  // BEGIN_SDK_36
  implementation(project(':expoview-abi36_0_0'))
  // END_SDK_36

  // This is not needed in shell apps, the Amplitude module will include this dep if installed
  implementation 'com.amplitude:android-sdk:2.23.2'

  // WHEN_PREPARING_SHELL_REMOVE_TO_HERE
  // WHEN_DISTRIBUTING_REMOVE_TO_HERE

  /* UNCOMMENT WHEN DISTRIBUTING
  api 'org.webkit:android-jsc:r245459' // needs to be before react-native
  api 'com.facebook.react:react-native:+'
  END UNCOMMENT WHEN DISTRIBUTING */

  // WHEN_DETACHING_REMOVE_FROM_HERE
  /* UNCOMMENT WHEN DISTRIBUTING
  addMavenUnimodulesDependencies([
      modulesPaths : [
        '../enabled-modules'
      ],
      configuration: 'api',
      target       : 'react-native',
      exclude      : [
        'expo-module-template',
        'expo-bluetooth',
        'expo-in-app-purchases',
        'expo-payments-stripe',
        'expo-updates',
        'expo-splash-screen',
        'expo-gl-cpp-legacy'
      ]
  ])
  END UNCOMMENT WHEN DISTRIBUTING */
  // WHEN_DETACHING_REMOVE_TO_HERE

  /* UNCOMMENT WHEN DETACHING
  addUnimodulesDependencies([
      modulesPaths : [
        '../../node_modules'
      ],
      configuration: 'api',
      target       : 'react-native',
      exclude      : [
        // You can exclude unneeded modules here, eg.
        // 'unimodules-face-detector-interface',
        // 'expo-face-detector'

        // Adding a name here will also remove the package
        // from auto-generated BasePackageList.java
      ]
  ])
  END UNCOMMENT WHEN DETACHING */
}

// This has to be down here for some reason
apply plugin: 'com.google.gms.google-services'
